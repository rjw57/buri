# Memory map for buri 6502 computer
MEMORY
{
    ZEROPAGE:   start=$0000, size=$0100, define=yes;            # Zero page
    STACK:      start=$0100, size=$0100, define=yes;            # Stack page
    OSDATA:     start=$0200, size=$0200, define=yes;            # OS workspace
    USR:        start=$0400, size=$7C00, define=yes;            # User memory
    IO:         start=$8000, size=$4000, define=yes;            # 16K of IO
    ROM:        start=$E000, size=$2000, file="%O";             # 8K of ROM
}

# Segments within the EEPROM
SEGMENTS
{
    # Initialised segments
    DATA:       load=ROM, run=OSDATA, type=rw, define=yes;      # OS read/write data
    RODATA:     load=ROM, type=ro;                              # Read-only data segment
    CODE:       load=ROM, type=ro;                              # ROM code
    VECTORS:    load=ROM, type=ro, start=$FFFA;                 # Processor vector table

    # Uninitialised segments
    BSS:        load=OSDATA, type=bss;                          # OS temp storage
    ZEROPAGE:   load=ZEROPAGE, type=zp;                         # Zero-page
    OSZP:       load=ZEROPAGE, type=zp, start=$D0;              # OS-section of zero page
}
