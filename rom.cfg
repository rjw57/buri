# Memory map for buri 6502 computer
MEMORY
{
    ZEROPAGE:   start=$0000, size=$0100, define=yes;            # Zero page
    STACK:      start=$0100, size=$0100, define=yes;            # Stack page
    OSDATA:     start=$0200, size=$0100, define=yes;            # OS workspace
    OSCSTACK:   start=$0300, size=$0100, define=yes;            # OS C call stack
    USR:        start=$0400, size=$7C00, define=yes;            # User memory
    IO:         start=$DF80, size=$0080, define=yes;            # 8x16 byte IO pages
    ROM:        start=$E000, size=$2000, file="%O";             # 8K of ROM
}

# Segments within the EEPROM
SEGMENTS
{
    # Initialised segments
    DATA:       load=ROM, run=OSDATA, type=rw, define=yes;      # OS read/write data
    CMDTBL:     load=ROM, type=ro, define=yes;                  # Command name table
    RODATA:     load=ROM, type=ro;                              # Read-only data segment
    CODE:       load=ROM, type=ro;                              # ROM code
    VECTORS:    load=ROM, type=ro, start=$FFE4;                 # Processor "native" vector table
    VECTORS_E:  load=ROM, type=ro, start=$FFF4;                 # Processor "emulation" vector table

    # Uninitialised segments
    BSS:        load=OSDATA, type=bss;                          # OS temp storage
    ZEROPAGE:   load=ZEROPAGE, type=zp;                         # Zero-page
    OSZP:       load=ZEROPAGE, type=zp, start=$D0;              # OS-section of zero page
}
